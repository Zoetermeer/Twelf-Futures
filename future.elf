% Author: James Swaine 
% Small-step semantics for Mini-ML with futures and explicit forcing.
% This supports nested futures.  

% p ::= e (e N) ...   (where N ranges over nats) 

nat : type. 
zero : nat. 
succ : nat -> nat.

nat_to_exp : nat -> exp -> type. 
%mode nat_to_exp +N -E.
nat_to_exp_zero : nat_to_exp zero z. 
nat_to_exp_succ : nat_to_exp (succ N) (s N') 
               <- nat_to_exp N N'.

exp_to_nat : exp -> nat -> type. 
%mode exp_to_nat +E -N. 
exp_to_nat_z : exp_to_nat z zero. 
exp_to_nat_s : exp_to_nat (s E) (succ N) 
               <- exp_to_nat E N.

% A thread is an exp * nat pair, where the nat is the future/thread id.
thread 	: type. 			  %name thread T.
mkthread : exp -> nat -> thread. 

% Thread list 
threads : type.         %name threads Ts.
nothreads : threads.
cth : thread -> threads -> threads. % cth = cons threads

% Gives the next available future/thread ID
nextdesc        : threads -> nat -> type. 
%mode nextdesc +Ts -M.
next_empty      : nextdesc nothreads zero. 
next_nonempt_z  : nextdesc (cth (mkthread E zero) Ts) (succ zero). 
next_nonempt_s  : nextdesc (cth (mkthread E (succ M)) Ts) (succ M).

% Whether the future has finished evaluation 
% May not really be necessary, since all we need is a 
% premise for V value
tdone           : thread -> exp -> type. 
tdone_v         : tdone (mkthread V M) V 
               <- value V.

% Look up a future in the list 
tlookup         : nat -> threads -> thread -> type.  
%mode tlookup +E +Ts -T.
tlookup_match   : tlookup M (cth (mkthread E M) Ts) (mkthread E M).
tlookup_recur   : tlookup M (cth (mkthread E M1) Ts) T
               <- tlookup M Ts T. 

% Program representation
prog : type. 						%name prog P.
mkprog : exp -> threads -> prog. 

% Judgments
progstep : prog -> prog -> type. 
%mode progstep +P -P'.
estp : exp -> threads -> exp -> threads -> type.
%mode estp +E +T -E' -T'.

progstep_noth   : progstep (mkprog E nothreads) (mkprog E' Ts) 
                <- estp E nothreads E' Ts.
progstep_th     : progstep (mkprog E Ts) (mkprog E' Ts') 
                <- estp E Ts E' Ts'.

% Two sets of inference rules here.  At any given point, 
% we can either choose to continue executing the 'current' thread 
% and move one step in evaluating its current E, 
% or swap in another thread and begin executing there. 
% The first set of rules covers the cases where we continue executing 
% on the 'current' thread.

% Continue to execute the 'current' thread
estp_cont_s     : estp (s E) Ts (s E') Ts 
               <- estp E Ts E' Ts.
estp_cont_case_z: estp (case z E1 E2) Ts (case z E1' E2) Ts
               <- estp E1 Ts E1' Ts.
estp_cont_case_s: estp (case (s V) E1 E2) Ts (E2 V) Ts 
               <- value V.
estp_cont_app   : estp (app E1 E2) Ts (app E1' E2) Ts 
               <- estp E1 Ts E1' Ts.
estp_cont_fix   : estp (fix E) Ts (E (fix E)) Ts.

% 'future' returns a value (the ID/descriptor of the new future), so the result is M
estp_cont_future: estp (future E) Ts N (cth (mkthread E M) Ts) 
               <- nextdesc Ts M
               <- nat_to_exp M N.
estp_cont_touch : estp (touch E) Ts (touch E') Ts' 
               <- estp E Ts E' Ts'.
estp_cont_touchv: estp (touch M) Ts V Ts 
               <- tlookup N Ts (mkthread V N)
               <- exp_to_nat M N
               <- value V.
estp_cont_touchu: estp (touch M) Ts E' Ts' 
               <- estp E Ts E' Ts'
               <- tlookup N Ts (mkthread E N) 
               <- exp_to_nat M N.               


% This set of rules covers the cases when we decide to switch to another thread.
estp_prpt_s     : estp E (cth (mkthread (s E1) N) Ts) E (cth (mkthread (s E1') N) Ts)
               <- estp E1 Ts E1' Ts'.
estp_prpt_case_z: estp E (cth (mkthread (case z E1 E2) N) Ts) E (cth (mkthread (case z E1' E2) N) Ts) 
               <- estp E1 Ts E1' Ts'. 
estp_prpt_case_s: estp E (cth (mkthread (case (s V) E1 E2) N) Ts) E (cth (mkthread (E2 V) N) Ts) 
               <- value V.
estp_prpt_app   : estp E (cth (mkthread (app E1 E2) N) Ts) E (cth (mkthread (app E1' E2) N) Ts) 
               <- estp E1 Ts E1' Ts.
estp_prpt_fix   : estp E (cth (mkthread (fix E1) M) Ts) E (cth (mkthread (E1 (fix E1)) N) Ts).
estp_prpt_future: estp E (cth (mkthread (future E) N1) Ts) E (cth (mkthread E N2) (cth (mkthread M2 N1) Ts))
               <- nextdesc Ts N2 
               <- nat_to_exp N2 M2.
estp_prpt_touch : estp E (cth (mkthread (touch E1) M) Ts) E (cth (mkthread (touch E1') M) Ts) 
               <- estp E1 Ts E1' Ts'.
estp_prpt_touchv: estp E (cth (mkthread (touch M) N1) Ts) E (cth (mkthread (touch M) N1) (cth (mkthread E1' N) Ts)) 
               <- estp E1 Ts E1' Ts' 
               <- tlookup N Ts (mkthread E1 N) 
               <- exp_to_nat M N.

% Top-level evaluation
eval            : exp -> exp -> type. 
%mode eval +E -E'.
eval_v          : eval V V 
               <- value V. 
eval_steps      : eval E E'' 
               <- progstep (mkprog E nothreads) (mkprog E' Ts) 
               <- eval E' E''.

